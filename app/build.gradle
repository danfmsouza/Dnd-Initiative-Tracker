apply plugin: 'com.android.application'

// Manifest version information major.minor.patch.hotfix
def versionMajor = 1  // Must be in [0, 99]
def versionMinor = 1  // Must be in [0, 99]
def versionPatch = 1  // Must be in [0, 99]
def versionHotfix = 0 // Must be in [0, 9]

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.keiferstone.dndinitiativetracker"

        minSdkVersion 21
        targetSdkVersion 25

        versionCode versionMajor * 100000 + versionMinor * 1000 + versionPatch * 10 + versionHotfix
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        releaseConfig {
            storeFile file(System.getenv("ANDROID_KEY_STORE"))
            storePassword System.getenv("ANDROID_KEY_STORE_PASSWORD")
            keyAlias System.getenv("ANDROID_KEY_ALIAS")
            keyPassword System.getenv("ANDROID_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.releaseConfig
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    // Local libraries
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android support libraries
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.0'

    // Third party libraries
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'
    compile 'com.google.code.gson:gson:2.8.0'

    // Test libraries
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}
